// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Chooseing value to register A
    And(a=instruction[15], b=instruction[5], out=isFromALU);
    Mux16(a=instruction, b=ALUOutput, sel=isFromALU, out=valueToRegA);
    
    // Write value in register A
    Not(in=instruction[15], out=isAInstruction);
    Or(a=isAInstruction, b=isFromALU, out=isNewValueForA);
    ARegister(in=valueToRegA, load=isNewValueForA, out=regA, out[0..14]=addressM);

    // Write value in register D
    And(a=instruction[15], b=instruction[4], out=isNewValueForD);
    DRegister(in=ALUOutput, load=isNewValueForD, out=regDOrAInputALU);

    // Chooseing value from A or M
    Mux16(a=regA, b=inM, sel=instruction[12], out=bInputALU);

    // writeM
    And(a=instruction[15], b=instruction[3], out=writeM);

    // ALU
    ALU(x=regDOrAInputALU, y=bInputALU, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOutput, out=outM, zr=equel0, ng=less0);
    
    // PC
    // Any jump
    Or(a=less0, b=equel0, out=less0OrEquel0);
    Not(in=less0OrEquel0, out=more0);

    And(a=instruction[2], b=less0, out=jlt);
    And(a=instruction[1], b=equel0, out=jeq);
    And(a=instruction[0], b=more0, out=jgt);

    Or(a=jlt, b=jeq, out=isAnyJump0);
    Or(a=jgt, b=isAnyJump0, out=isAnyJump);
    And(a=instruction[15], b=isAnyJump, out=needLoad);

    PC(in=regA, load=needLoad, inc=true, reset=reset, out[0..14]=pc);

}